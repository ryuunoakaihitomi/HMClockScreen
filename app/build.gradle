apply plugin: 'com.android.application'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-android'
apply plugin: 'AndResGuard'

android {
    signingConfigs {
        arg {
            Properties p = new Properties()
            File f = new File(rootDir, "signInfo.properties")
            if (f.exists()) {
                p.load(new FileReader(f))
                keyAlias p['KEY_ALIAS']
                keyPassword p['KEY_PWD']
                storePassword p['STORE_PWD']
                storeFile file(p['STORE_FILE'])
            } else println 'If you want to build the app in a smaller size with AndResGuard,' +
                    'you must create signInfo.properties before.'
        }
    }
    compileSdkVersion 29
    defaultConfig {
        applicationId "github.ryuunoakaihitomi.hmclockscreen"
        minSdkVersion 19
        targetSdkVersion 29
        versionCode 13
        versionName "1.10.2"
        resConfigs "en"
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.arg
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}

android.applicationVariants.all { variant ->
    variant.outputs.all {
        outputFileName = "${rootProject.name}-${variant.versionName}_${variant.buildType.name}.apk"
    }
}

andResGuard {
    mappingFile = null
    use7zip = true
    useSign = true
    keepRoot = false
    fixedResName = "x"
    mergeDuplicatedRes = true
    whiteList = [
            "R.mipmap.ic_launcher",
            "R.mipmap.ic_launcher_round"
    ]
    compressFilePattern = [
            "*.png"
    ]
    sevenzip {
        artifact = 'com.tencent.mm:SevenZip:1.2.17'
    }
    digestalg = "SHA-256"
}
